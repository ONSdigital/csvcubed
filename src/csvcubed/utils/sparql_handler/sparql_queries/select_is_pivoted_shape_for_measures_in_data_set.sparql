PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX csvw: <http://www.w3.org/ns/csvw#>
PREFIX qb: <http://purl.org/linked-data/cube#>

SELECT ?csvUrl ?measure (max(?isPivoted) as ?isPivotedShape)
WHERE {
    ?dsd a qb:DataStructureDefinition;
         qb:component/qb:measure ?measure.

    # N.B. The join between ?dsd and ?csvUrl is injected as a `VALUES` table when the query is executed.

    [] csvw:url ?csvUrl;
       csvw:tableSchema/csvw:column/rdf:rest*/rdf:first [ csvw:propertyUrl ?measureColumnPropertyUrl ].

    BIND((STRENDS(str(?measureColumnPropertyUrl), str(?measure)) || STRENDS(str(?measure), str(?measureColumnPropertyUrl))) as ?isPivoted).
}
GROUP BY ?csvUrl ?measure
